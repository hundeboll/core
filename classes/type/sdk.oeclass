## Typeclass for sdk recipes
##
## When building a sdk, everything that has to executed on the host when
## running has to be sdk recipes, except if they are cross-compiling from sdk
## to target, in which case they are sdk-cross. This recipe sets up the
## required variables for building sdk recipes.

HOST_TYPE		= "sdk"
TARGET_TYPE		= "sdk"
HOST_CROSS		= "sdk-cross"
TARGET_CROSS		= "sdk-cross"

RECIPE_ARCH		= "${SDK_ARCH}"

# Set host=sdk and target=sdk for architecture triplet build/sdk/sdk
HOST_ARCH		= "${SDK_ARCH}"
TARGET_ARCH		= "${SDK_ARCH}"

require conf/paths/sdk.conf
require conf/paths/sdk-host.conf
require conf/paths/sdk-target.conf

# Provide information about the sdk cross target
addhook set_machine to post_recipe_parse first before blacklist
def set_machine(d):
    d["SDK_MACHINE_ARCH"] = d.get("MACHINE_ARCH")

BLACKLIST_VAR += "MACHINE"
BLACKLIST_PREFIX += "MACHINE_"

PATH =. "${STAGE_PATH}:"
STAGE_PATH = "\
${STAGE_DIR}/sdk-cross${stage_bindir}:\
${STAGE_DIR}/native${stage_bindir}\
"

LD_LIBRARY_PATH = "\
${STAGE_DIR}/sdk-cross${stage_libdir}:\
${STAGE_DIR}/native${stage_libdir}\
"

export CROSS	= "${HOST_PREFIX}"

inherit sdk-static

# Local Variables:
# mode: python
# End:
